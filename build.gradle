// https://www.jenkins.io/doc/book/pipeline/shared-libraries/
apply plugin: 'groovy'

group = 'org.example'
version = '1.0-SNAPSHOT'

// follow the structure as dictated by Jenkins:
sourceSets {
    main {
        groovy {
            srcDirs = ['src','vars']
        }
        resources {
            srcDirs = ['resources']
        }
    }
}

tasks.withType(GroovyCompile) {
    groovyOptions.encoding = 'UTF-8'
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url "https://repo.jenkins-ci.org/releases/"
    }
}

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:2.5.6'
    implementation 'org.apache.ivy:ivy:2.4.0'
    implementation 'org.jenkins-ci.main:jenkins-core:2.164.1'
    def staplerGAV = 'org.kohsuke.stapler:stapler:1.255'
    implementation staplerGAV
    annotationProcessor staplerGAV
    implementation 'org.jenkins-ci.plugins.workflow:workflow-step-api:2.19@jar'
    implementation 'org.jenkins-ci.plugins:pipeline-utility-steps:2.2.0@jar'
    testImplementation  'org.spockframework:spock-core:1.3-groovy-2.5'
    testRuntimeOnly 'net.bytebuddy:byte-buddy:1.9.12'
    testRuntimeOnly 'org.objenesis:objenesis:3.0.1'
    testImplementation  'junit:junit:4.12'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.4.1'
    testImplementation  'com.lesfurets:jenkins-pipeline-unit:1.1'
}